apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.1'

    defaultConfig {
        applicationId "free.rm.skytube"
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 12
        // Third party devs:  pls be ethical and change the below keys
        buildConfigField 'String[]', 'YOUTUBE_API_KEYS',
                '{' +
                        '"AIzaSyBlD0IyrnYDVUklu-lEp5ZPmeUFaKEmz2I",' +
                        '"AIzaSyCBe6h-D27WgcDkX9Wuyb3Fjef6tFja2Eg",' +
                        '"AIzaSyC2eifJuPidYxsq7NKvZGdTxngFrhbm0G0"' +
                        '}'
        buildConfigField 'String', 'SKYTUBE_WEBSITE', '"http://skytube-app.com"'
        buildConfigField 'String', 'SKYTUBE_WEBSITE_CREDITS', '"http://skytube-app.com/credits.html"'
        buildConfigField 'String', 'SKYTUBE_UPDATES_URL', '"https://api.github.com/repos/ram-on/SkyTube/releases/latest"'
        vectorDrawables.useSupportLibrary true
        multiDexEnabled true    // as we have over 65536 lines of code...
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "skytube"
    productFlavors {
        // 100% Open-Source Edition
        oss {
            applicationId "free.rm.skytube.oss"
            versionName "2.81"          // digits of the *fractional part* shall NOT be > than "9"
            resValue "string", "app_name", "SkyTube"
            dimension "skytube"
        }
        // 99% Open-source edition:  uses the YouTube Player Jar [i.e. proprietary module - optional usage]
        extra {
            applicationId "free.rm.skytube.extra"
            versionName "2.81 Extra"    // digits of the *fractional part* shall NOT be > than "9"
            resValue "string", "app_name", "SkyTube Extra"
            dimension "skytube"
        }
    }
}

configurations {
    nonFreeCompile
}

dependencies {
    testImplementation 'junit:junit:4.12'

    implementation group: 'org.ocpsoft.prettytime', name: 'prettytime', version: '4.0.1.Final'
    implementation 'com.google.http-client:google-http-client-android:1.23.0'
    implementation 'com.google.apis:google-api-services-youtube:v3-rev187-1.23.0'

    implementation 'com.github.TeamNewPipe:NewPipeExtractor:v0.11.5'
    implementation 'com.github.moraisigor:slidingdrawer:1.6.7'
    implementation 'com.github.bumptech.glide:glide:4.3.1'
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation group: 'joda-time', name: 'joda-time', version: '2.9.9'
    implementation 'com.afollestad.material-dialogs:core:0.9.5.0'
    implementation 'com.github.angads25:filepicker:1.1.1'

    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support:support-v13:27.0.2'
    implementation 'com.android.support:design:27.0.2'
    implementation 'com.android.support:recyclerview-v7:27.0.2'
    implementation 'com.android.support:cardview-v7:27.0.2'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:multidex:1.0.2' // as we have over 65536 lines of code...

    // proprietary module
    nonFreeCompile files('libs/YouTubeAndroidPlayerApi.jar')
}




