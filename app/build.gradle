apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'

    defaultConfig {
        applicationId "free.rm.skytube"
        minSdkVersion 14
        targetSdkVersion 28

        versionName "2.973"    // digits of the *fractional part* shall NOT be > than "9"
        versionCode 33
        // Third party devs:  pls be ethical and change the below keys
        //buildConfigField 'String',   'YOUTUBE_API_KEYS_DEBUG', '"AIzaSyDU-ZZCyOB_kYkiMSZ6ooSipUZRukHU4ik"'
        buildConfigField 'String',   'YOUTUBE_API_KEYS_DEBUG', '"hidden:A0C0B03EED58193F4CD52DF668495D523A5229C6B66B0ED6282AE09BD8FF08CD5AF9A6B52C214F"'
        buildConfigField 'String[]', 'YOUTUBE_API_KEYS',
                '{' +
                        '"hidden:A0C0B03EED581E1E18E900CF3E365C70253320C5D8040BF53818F2EAF4D62AEC6FA7E68F5F2175",' +
                        '"hidden:A0C0B03EED581E277FCC2BD47E75526C06401FB4F66E3EDB2A2BE2B7F9C606896DDEF9875F3804",' +
                        '"hidden:A0C0B03EED58183878F501C639336957395D24B2CC7C2FD43919CB92F682408C4CBEC1C2280F4B"' +
                '}'
        buildConfigField 'String', 'SKYTUBE_WEBSITE',         '"https://skytube-app.com"'
        buildConfigField 'String', 'SKYTUBE_WEBSITE_CREDITS', '"https://skytube-app.com/credits.html"'
        buildConfigField 'String', 'SKYTUBE_UPDATES_URL', '"https://api.github.com/repos/ram-on/SkyTube/releases/latest"'
        buildConfigField 'long', 'BUILD_TIME', String.valueOf(System.currentTimeMillis()) + 'L'
        vectorDrawables.useSupportLibrary true
        multiDexEnabled true    // as we have over 65536 lines of code...
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "CHROMECAST_APP_ID", "\"39E8D587\""
        }
        debug {
//            buildConfigField "String", "CHROMECAST_APP_ID", "\"39E8D587\"" // prod
            buildConfigField "String", "CHROMECAST_APP_ID", "\"29A66E17\"" // dev
        }
    }
    flavorDimensions "skytube"
    productFlavors {
        // 100% Open-Source Edition
        oss {
            applicationId "free.rm.skytube.oss"
            resValue "string", "app_name", "SkyTube"
            dimension "skytube"
            buildConfigField 'String[]', 'YOUTUBE_API_KEYS',
                    '{' + // new keys
                        '"hidden:A0C0B03EED581E1E18E900CF3E365C70253320C5D8040BF53818F2EAF4D62AEC6FA7E68F5F2175",' +
                        '"hidden:A0C0B03EED581E277FCC2BD47E75526C06401FB4F66E3EDB2A2BE2B7F9C606896DDEF9875F3804",' +
                        '"hidden:A0C0B03EED58183878F501C639336957395D24B2CC7C2FD43919CB92F682408C4CBEC1C2280F4B",' +
                        // old keys, but this can be used with FDroid
                        '"AIzaSyC27jgPDHkQsQK8TW4ngrQprSCnlMTJ04w",' +
                        '"AIzaSyCmXsaE6ZW14-1vM5zceWnGWnB73FkY7U4",' +
                        '"AIzaSyBd3Loyy_XN2tPHPfc1E7EkSkdCqfzTHxg"'  +
                    '}'
        }
        // 99% Open-source edition:  uses the YouTube Player Jar [i.e. proprietary module - optional usage]
        extra {
            applicationId "free.rm.skytube.extra"
            resValue "string", "app_name", "SkyTube Extra"
            dimension "skytube"
        }
    }
    // Generate APK filenames as follows:  <App Name>-<Flavor><Version>
    applicationVariants.all { variant ->
        outputs.all { output ->
            outputFileName = "${parent.name}-${productFlavors[0].name.capitalize()}-${versionName}.apk"
        }
    }
    // Ignore missing translations
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    dexOptions {
        jumboMode true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        exclude "META-INF/*"
    }
}

dependencies {
    // YouTube modules
    implementation 'com.github.SkyTubeTeam.google-http-java-client:google-http-client-android:1.34.3.1'

    implementation ('com.google.apis:google-api-services-youtube:v3-rev20200423-1.30.9') {
        exclude group: 'com.google.http-client', module:'google-http-client'
        exclude group: 'com.google.http-client', module:'google-http-client-jackson2'
    }

    // implementation 'com.github.TeamNewPipe.NewPipeExtractor:extractor:v0.19.0'
    implementation 'com.github.SkyTubeTeam.NewPipeExtractor:extractor:2aadfc44790b30a212'

    // other modules
    implementation 'org.ocpsoft.prettytime:prettytime:4.0.5.Final'
    implementation 'com.github.moraisigor:slidingdrawer:1.7.1'
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'
    implementation 'com.jakewharton:butterknife:10.2.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.1'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'joda-time:joda-time:2.10.6'
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.google.android.exoplayer:exoplayer:2.9.6'
    implementation 'org.jsoup:jsoup:1.13.1'
    implementation ('com.optimaize.languagedetector:language-detector:0.6') {
        exclude group: 'com.google.guava', module: 'guava'
        exclude group: 'com.intellij', module: 'annotations'
    }
    implementation 'com.mikepenz:actionitembadge:4.0.0@aar'
    implementation 'com.mikepenz:iconics-core:4.0.2@aar'
    implementation 'com.github.hedzr:android-file-chooser:v1.2.0-final'

    // Android support modules
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.core:core:1.2.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.multidex:multidex:2.0.1' // as we have over 65536 lines of code...

    // debugImplementation because LeakCanary should only run in debug builds.
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.4'

    // proprietary module
    extraImplementation files('libs/YouTubeAndroidPlayerApi.jar')
    extraImplementation 'com.google.android.gms:play-services-cast-framework:19.0.0'
    extraImplementation 'com.sothree.slidinguppanel:library:3.4.0'
    extraImplementation 'com.github.rahatarmanahmed:circularprogressview:2.5.0'
    extraImplementation 'com.squareup.picasso:picasso:2.71828'
}
